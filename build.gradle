plugins {
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.vanniktech.maven.publish' version '0.34.0'
    id 'net.researchgate.release' version '3.1.0'
}

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

group = 'com.digitalsanctuary'
// version = '3.0.0-SNAPSHOT'
description = 'Spring User Framework'

ext {
    springBootVersion = '3.5.5'
    lombokVersion = '1.18.40'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
	mavenCentral()
}

dependencyUpdates {
    // Only look at release versions
    revision = 'release'

    // Reject non-stable versions (alpha, beta, rc, milestone, etc.)
    rejectVersionIf {
        isNonStable(candidate.version) && !isNonStable(currentVersion)
    }
}

boolean isNonStable(String version) {
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { kw ->
        version.toUpperCase().contains(kw)
    }
    def regex = /^[0-9,.v-]+(-r)?$/ // matches stable versions like 1.2.3, 2.0, 1.0-r
    return !stableKeyword && !(version ==~ regex)
}

dependencies {
    // Spring Boot starters
    compileOnly 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.springframework.boot:spring-boot-starter-jdbc'
    compileOnly 'org.springframework.boot:spring-boot-starter-mail'
    compileOnly 'org.springframework.boot:spring-boot-starter-oauth2-client'
    compileOnly 'org.springframework.boot:spring-boot-starter-security'
    compileOnly 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compileOnly 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6:3.1.3.RELEASE'
    compileOnly 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.4.0'

    // Other dependencies (moved to test scope for library)
    implementation 'org.passay:passay:1.6.6'
    implementation 'com.google.guava:guava:33.5.0-jre'
    compileOnly 'jakarta.validation:jakarta.validation-api:3.1.1'
    compileOnly 'org.springframework.retry:spring-retry'

    // Lombok dependencies
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Lombok dependencies for test classes
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

     // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'org.springframework.boot:spring-boot-starter-mail'
    testImplementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    testImplementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.retry:spring-retry'
    testImplementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    testImplementation 'com.h2database:h2:2.3.232'

    // Runtime dependencies moved to test scope for library
    testRuntimeOnly 'org.springframework.boot:spring-boot-devtools'
    testRuntimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    testRuntimeOnly 'org.postgresql:postgresql'

    // Additional test dependencies for improved testing
    testImplementation 'org.testcontainers:testcontainers:1.21.3'
    testImplementation 'org.testcontainers:mariadb:1.21.3'
    testImplementation 'com.github.tomakehurst:wiremock:3.0.1'
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.4.1'
    testImplementation 'org.assertj:assertj-core:3.27.4'
    testImplementation 'io.rest-assured:rest-assured:5.5.6'
    testImplementation 'com.icegreen:greenmail:2.1.5'
    testImplementation 'org.awaitility:awaitility:4.3.0'
}

tasks.named('bootJar') {
    enabled = false
}

test {
	useJUnitPlatform()
	// Enable parallel execution for faster test runs
	systemProperty 'junit.jupiter.execution.parallel.enabled', 'true'
	systemProperty 'junit.jupiter.execution.parallel.mode.default', 'concurrent'
	systemProperty 'junit.jupiter.execution.parallel.mode.classes.default', 'concurrent'
	// Use available CPU cores - 1 for parallel execution
	systemProperty 'junit.jupiter.execution.parallel.config.strategy', 'dynamic'
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
	  testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        exceptionFormat "full"
        showStandardStreams true
    }
}

tasks.named('jar') {
    enabled = true
    archiveBaseName.set('ds-spring-user-framework')
    archiveClassifier.set('')
}

def registerJdkTestTask(name, jdkVersion) {
    tasks.register(name, Test) {
        javaLauncher.set(javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(jdkVersion)
        })
        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets.test.runtimeClasspath
        useJUnitPlatform()
        // Enable parallel execution for JDK-specific tests
        systemProperty 'junit.jupiter.execution.parallel.enabled', 'true'
        systemProperty 'junit.jupiter.execution.parallel.mode.default', 'concurrent'
        systemProperty 'junit.jupiter.execution.parallel.mode.classes.default', 'concurrent'
        systemProperty 'junit.jupiter.execution.parallel.config.strategy', 'dynamic'
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
        }
        doFirst {
            println("Running tests with JDK $jdkVersion")
        }
    }
}

registerJdkTestTask('testJdk17', 17)
registerJdkTestTask('testJdk21', 21)


// Optional task that runs tests with multiple JDKs
tasks.register('testAll') {
    dependsOn(tasks.named('testJdk17'), tasks.named('testJdk21'))
    doFirst {
        println("Running tests with both JDK 17 and JDK 21")
    }
}


// Maven Central Publishing Tasks
mavenPublishing {
  configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
  // publishToMavenCentral() is now configured via gradle.properties
  signAllPublications()
  coordinates("com.digitalsanctuary", "ds-spring-user-framework", project.version)

  pom {
    name = "DS Spring User Framework"
    description = "Simple SpringBoot User Library built on top of Spring Security."
    inceptionYear = "2024"
    url = "https://github.com/devondragon/SpringUserFramework"
    licenses {
      license {
        name = "The Apache License, Version 2.0"
        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }
    }
    developers {
      developer {
        id = "devondragon"
        name = "Devon Hillard"
        url = "https://github.com/devondragon/"
      }
    }
    scm {
      url = "https://github.com/devondragon/SpringUserFramework"
      connection = "scm:git:git@github.com:devondragon/SpringUserFramework.git"
      developerConnection = "scm:git:ssh://git@github.com:devondragon/SpringUserFramework.git"
    }
  }
}

tasks.named("publishMavenPublicationToMavenCentralRepository") {
    dependsOn("signMavenPublication")
}

publishing {
    repositories {
        maven {
            name = 'reposiliteRepository'
            url = uri('https://reposilite.tr0n.io/private')
            credentials(PasswordCredentials)
            authentication {
                 basic(BasicAuthentication)
            }
        }
        // more repositories can go here
    }
}

// Maven Publishing Aliases

tasks.register("publishReposilite") {
    dependsOn("publishMavenPublicationToReposiliteRepository")
}

tasks.register("publishMavenCentral") {
    dependsOn("publishAndReleaseToMavenCentral")
}

tasks.register("publishLocal") {
    dependsOn("publishToMavenLocal")
}

task generateAIChangelog(type: Exec) {
    def newVersion = project.version
    commandLine 'mise', 'x', '--', 'python', 'generate_changelog.py', newVersion
}

release {
    beforeReleaseBuild.dependsOn generateAIChangelog
    // afterReleaseBuild.dependsOn publishReposilite
    afterReleaseBuild.dependsOn publishMavenCentral
}
